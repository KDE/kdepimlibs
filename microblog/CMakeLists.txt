cmake_minimum_required(VERSION 2.8.12)

project(MicroBlog)

# ECM setup
find_package(ECM 0.0.11 CONFIG REQUIRED)
set(CMAKE_MODULE_PATH ${ECM_MODULE_PATH})

include(GenerateExportHeader)
include(ECMGenerateHeaders)
include(ECMGeneratePriFile)
include(ECMPackageConfigHelpers)
include(ECMSetupVersion)
include(FeatureSummary)
include(KDEInstallDirs)
include(KDECMakeSettings)
include(KDEFrameworkCompilerSettings)


set(KF5_VERSION "4.97.0")

ecm_setup_version(${KF5_VERSION} VARIABLE_PREFIX MICROBLOG
                        VERSION_HEADER "${CMAKE_CURRENT_BINARY_DIR}/microblog_version.h"
                        PACKAGE_VERSION_FILE "${CMAKE_CURRENT_BINARY_DIR}/KF5MicroBlogConfigVersion.cmake"
                        SOVERSION 5
)

########### Find packages ###########
# FIXME: remove this if/endif when Kdepimlibs is split.
if ("${CMAKE_SOURCE_DIR}" STREQUAL "${CMAKE_CURRENT_SOURCE_DIR}")
  find_package(KF5PimUtils ${KF5_VERSION} CONFIG REQUIRED)
endif()

########### Targets ###########
add_subdirectory(src)

########### CMake Config Files ###########
set(CMAKECONFIG_INSTALL_DIR "${CMAKECONFIG_INSTALL_PREFIX}/KF5MicroBlog")

ecm_configure_package_config_file(
  "${CMAKE_CURRENT_SOURCE_DIR}/KF5MicroBlogConfig.cmake.in"
  "${CMAKE_CURRENT_BINARY_DIR}/KF5MicroBlogConfig.cmake"
  INSTALL_DESTINATION ${CMAKECONFIG_INSTALL_DIR}
)

install(FILES
  "${CMAKE_CURRENT_BINARY_DIR}/KF5MicroBlogConfig.cmake"
  "${CMAKE_CURRENT_BINARY_DIR}/KF5MicroBlogConfigVersion.cmake"
  DESTINATION "${CMAKECONFIG_INSTALL_DIR}"
  COMPONENT Devel
)

install(EXPORT KF5MicroBlogTargets
    DESTINATION "${CMAKECONFIG_INSTALL_DIR}"
    FILE KF5MicroBlogTargets.cmake
    NAMESPACE KF5::
)

install(FILES
   ${CMAKE_CURRENT_BINARY_DIR}/microblog_version.h
   DESTINATION ${INCLUDE_INSTALL_DIR}
   COMPONENT Devel
)

if ("${CMAKE_BINARY_DIR}" STREQUAL "${MicroBlog_BINARY_DIR}")
  feature_summary(WHAT ALL FATAL_ON_MISSING_REQUIRED_PACKAGES)
endif()
